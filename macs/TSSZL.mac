!2019.04.10
!独立塔身\底座十字梁\格构柱基础
!
! *DO,JJ,1,8,1   !循环8工况求解开始
JJ=2
PARSAV  !清空前保存标量参数
/CLEAR
PARRES  !清空后释放标量参数
/PREP7

!标准节类型：人字+顺斜腹杆
!
!Z+为向上方向
!
!定义单元类型、材料属性
ET,1,BEAM188
MP,EX,1,2.1E5
MP,PRXY,1,0.3
MP,DENS,1,7.85E-9
!1. 定义立柱截面，H型钢
w1=350
w2=350
t1=19
t2=12
SECTYPE,1,BEAM,I,,0
SECOFFSET,CENT
SECDATA,w1,w1,w2,t1,t1,t2
!2. 定义腹杆截面，角钢
b1=140
t3=14
SECTYPE,2,BEAM,L,,0
SECOFFSET,CENT
SECDATA,b1,b1,t3,t3
!3. 定义顶部十字梁，矩形
b2=200
SECTYPE,3,BEAM,RECT,,0
SECOFFSET,CENT
SECDATA,b2,b2
!4. 底座十字梁，箱型梁
GW1=500   !宽
GH1=1000  !高
GT1=50     !上下板厚
GT2=30     !左右板厚
SECTYPE,4,BEAM,HREC,,0
SECOFFSET,USER,GW1/2,GH1/2
SECDATA,GW1,GH1,GT2,GT2,GT1,GT1

!5. 格构柱立柱，方管/圆管
GW2=600
GT3=16
SECTYPE,5,BEAM,HREC,,0
SECOFFSET,USER,GW2/2,GW2/2
SECDATA,GW2,GW2,GT3,GT3,GT3,GT3

!SECTYPE,5,BEAM,CTUBE,,0
!SECOFFSET,CENT
!SECDATA,GW2/2-GT3,GW2/2

!6. 格构柱水平撑杆，斜撑杆
SECTYPE,6,BEAM,L,,0
SECOFFSET,CENT
SECDATA,200,200,16,16


!定义塔身参数
L1=2350  !标准节中心距
H1=2000  !标准节每档高度
N1=14    !标准节数量
N2=N1*2  !标准节档数


!定义载荷,单位：吨、吨米
!垂直力，扣除塔身自重后
FFZ=240 !240-4.3*14+12*1
!水平力，施加在底部
FFH=8 !7.5
!不平衡弯矩，等效施加在顶部
MMT=1036 !975
!扭矩
MK=30

!定义标准节节点
*DO,i,1,(N2+1)
N,((i-1)*8+1),L1/2 ,L1/2 ,(i-1)*H1
N,((i-1)*8+2),L1/2 ,0    ,(i-1)*H1
N,((i-1)*8+3),L1/2 ,-L1/2,(i-1)*H1
N,((i-1)*8+4),0    ,-L1/2,(i-1)*H1
N,((i-1)*8+5),-L1/2,-L1/2,(i-1)*H1
N,((i-1)*8+6),-L1/2,0    ,(i-1)*H1
N,((i-1)*8+7),-L1/2,L1/2 ,(i-1)*H1
N,((i-1)*8+8),0    ,L1/2 ,(i-1)*H1
*ENDDO
N,((N2+1)*8+1),0,0,N2*H1  !顶部中心点

NUMB=(N2+1)*8+1

!十字梁
L2=4000  !格构柱中心距


N,NUMB+1,0,0,-GH1/2
!!!!!!!第一象限短梁
*DO,i,1,9
N,NUMB+9+i,i*L1/20,i*L1/20,-GH1/2
*ENDDO

N,NUMB+2,L1/2,L1/2,-GH1/2

*DO,i,1,9
N,NUMB+18+i,i*(L2-L1)/20+L1/2,i*(L2-L1)/20+L1/2,-GH1/2
*ENDDO

N,NUMB+6,L2/2,L2/2,-GH1/2
!!!!!!!第四象限短梁
*DO,i,1,9
N,NUMB+27+i,i*L1/20,-i*L1/20,-GH1/2
*ENDDO
N,NUMB+3,L1/2,-L1/2,-GH1/2
*DO,i,1,9
N,NUMB+36+i,i*(L2-L1)/20+L1/2,-i*(L2-L1)/20-L1/2,-GH1/2
*ENDDO
N,NUMB+7,L2/2,-L2/2,-GH1/2
!!!!!!!!第三象限短梁
*DO,i,1,9
N,NUMB+45+i,-i*L1/20,-i*L1/20,-GH1/2
*ENDDO
N,NUMB+4,-L1/2,-L1/2,-GH1/2
*DO,i,1,9
N,NUMB+54+i,-i*(L2-L1)/20-L1/2,-i*(L2-L1)/20-L1/2,-GH1/2
*ENDDO
N,NUMB+8,-L2/2,-L2/2,-GH1/2
!!!!!!!!第二象限短梁
*DO,i,1,9
N,NUMB+63+i,-i*L1/20,i*L1/20,-GH1/2
*ENDDO
N,NUMB+5,-L1/2,L1/2,-GH1/2
*DO,i,1,9
N,NUMB+72+i,-i*(L2-L1)/20-L1/2,i*(L2-L1)/20+L1/2,-GH1/2
*ENDDO
N,NUMB+9,-L2/2,L2/2,-GH1/2
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
NUMB2=NUMB+81
!格构柱

ZH=1000 !格构柱高度
ZN=1   !格构柱水平撑数量，类似于档数

*DO,i,1,(ZN+1)
N,((i-1)*4+NUMB2+1),L2/2 ,L2/2 ,-(i-1)*ZH/ZN-GH1
N,((i-1)*4+NUMB2+2),L2/2 ,-L2/2 ,-(i-1)*ZH/ZN-GH1
N,((i-1)*4+NUMB2+3),-L2/2 ,-L2/2 ,-(i-1)*ZH/ZN-GH1
N,((i-1)*4+NUMB2+4),-L2/2 ,L2/2 ,-(i-1)*ZH/ZN-GH1
*ENDDO


!建立单元
TYPE,1
MAT,1
!1、立柱单元
SECNUM,1
*DO,i,1,N2
E,((i-1)*8+1),(i*8+1)
E,((i-1)*8+3),(i*8+3)
E,((i-1)*8+5),(i*8+5)
E,((i-1)*8+7),(i*8+7)
*ENDDO    !N2*4个单元
!2、腹杆单元
SECNUM,2
!2-1、横腹杆
*DO,i,1,(N2+1)
E,((i-1)*8+1),((i-1)*8+2)
E,((i-1)*8+2),((i-1)*8+3)
E,((i-1)*8+3),((i-1)*8+4)
E,((i-1)*8+4),((i-1)*8+5)
E,((i-1)*8+5),((i-1)*8+6)
E,((i-1)*8+6),((i-1)*8+7)
E,((i-1)*8+7),((i-1)*8+8)
E,((i-1)*8+8),((i-1)*8+1)
*ENDDO  !(N2+1)*8个单元
!2-2、斜腹杆
*DO,i,1,N2
E,((i-1)*8+1),(i*8+2)
E,((i-1)*8+3),(i*8+2)
E,((i-1)*8+5),(i*8+6)
E,((i-1)*8+7),(i*8+6)

E,((i-1)*8+1),(i*8+7)
E,((i-1)*8+5),(i*8+3)
*ENDDO  !N2*6个单元
!3、顶部十字单元
SECNUM,3
E,((N2+1)*8+1),(N2*8+1)
E,((N2+1)*8+1),(N2*8+3)
E,((N2+1)*8+1),(N2*8+5)
E,((N2+1)*8+1),(N2*8+7)!4个单元

!底部十字梁
SECNUM,4
!!!!!!!第一象限短梁
E,NUMB+1,NUMB+9+1
*DO,i,1,8
E,NUMB+9+i,NUMB+9+i+1
*ENDDO
E,NUMB+9+9,NUMB+2

E,NUMB+2,NUMB+18+1
*DO,i,1,8
E,NUMB+18+i,NUMB+18+i+1
*ENDDO
E,NUMB+18+9,NUMB+6

!!!!!!!第四象限短梁
E,NUMB+1,NUMB+27+1
*DO,i,1,8
E,NUMB+27+i,NUMB+27+i+1
*ENDDO
E,NUMB+27+9,NUMB+3

E,NUMB+3,NUMB+36+1
*DO,i,1,8
E,NUMB+36+i,NUMB+36+i+1
*ENDDO
E,NUMB+36+9,NUMB+7


!!!!!!!!第三象限短梁
E,NUMB+1,NUMB+45+1
*DO,i,1,8
E,NUMB+45+i,NUMB+45+i+1
*ENDDO
E,NUMB+45+9,NUMB+4

E,NUMB+4,NUMB+54+1
*DO,i,1,8
E,NUMB+54+i,NUMB+54+i+1
*ENDDO
E,NUMB+54+9,NUMB+8

!!!!!!!!第二象限短梁
E,NUMB+1,NUMB+63+1
*DO,i,1,8
E,NUMB+63+i,NUMB+63+i+1
*ENDDO
E,NUMB+63+9,NUMB+5

E,NUMB+5,NUMB+72+1
*DO,i,1,8
E,NUMB+72+i,NUMB+72+i+1
*ENDDO
E,NUMB+72+9,NUMB+9

!!!!!!!!!!!!!!!!!!!!!!!!!!
!80个单元

!4、格构柱主弦，单元号从N2*18+93到N2*18+92+ZN*16
SECNUM,5

*DO,i,1,ZN
E,((i-1)*4+NUMB2+1),(i*4+NUMB2+1)
E,((i-1)*4+NUMB2+2),(i*4+NUMB2+2)
E,((i-1)*4+NUMB2+3),(i*4+NUMB2+3)
E,((i-1)*4+NUMB2+4),(i*4+NUMB2+4)
*ENDDO    !ZN*4个单元

!格构柱水平撑斜撑
SECNUM,6
*DO,i,1,ZN
E,((i-1)*4+NUMB2+1),((i-1)*4+NUMB2+2)
E,((i-1)*4+NUMB2+2),((i-1)*4+NUMB2+3)
E,((i-1)*4+NUMB2+3),((i-1)*4+NUMB2+4)
E,((i-1)*4+NUMB2+4),((i-1)*4+NUMB2+1)
*ENDDO     !ZN*4个单元

*DO,i,1,ZN,2
E,((i-1)*4+NUMB2+1),(i*4+NUMB2+2)
E,((i-1)*4+NUMB2+1),(i*4+NUMB2+4)



E,((i-1)*4+NUMB2+3),(i*4+NUMB2+4)
E,((i-1)*4+NUMB2+3),(i*4+NUMB2+2)


*ENDDO   !ZN*4个单元

*DO,i,2,ZN,2
E,((i-1)*4+NUMB2+2),(i*4+NUMB2+3)
E,((i-1)*4+NUMB2+2),(i*4+NUMB2+1)

E,((i-1)*4+NUMB2+4),(i*4+NUMB2+1)
E,((i-1)*4+NUMB2+4),(i*4+NUMB2+3)
*ENDDO  !ZN*4个单元



/ESHAPE,1,1
!定义边界条件

!底部约束

*DO,i,1,4
D,NUMB2+i+ZN*4,UX,0
D,NUMB2+i+ZN*4,UY,0
D,NUMB2+i+ZN*4,UZ,0
*ENDDO

!D,NUMB2+1+ZN*4,ALL,0
!D,NUMB2+2+ZN*4,ALL,0
!D,NUMB2+3+ZN*4,ALL,0
!D,NUMB2+4+ZN*4,ALL,0


!节点耦合
CP,1,UX,1,NUMB+2
CP,2,UY,1,NUMB+2
CP,3,UZ,1,NUMB+2
CP,4,ROTX,1,NUMB+2
CP,5,ROTY,1,NUMB+2
CP,6,ROTZ,1,NUMB+2

CP,7,UX,3,NUMB+3
CP,8,UY,3,NUMB+3
CP,9,UZ,3,NUMB+3
CP,10,ROTX,3,NUMB+3
CP,11,ROTY,3,NUMB+3
CP,12,ROTZ,3,NUMB+3

CP,13,UX,5,NUMB+4
CP,14,UY,5,NUMB+4
CP,15,UZ,5,NUMB+4
CP,16,ROTX,5,NUMB+4
CP,17,ROTY,5,NUMB+4
CP,18,ROTZ,5,NUMB+4

CP,19,UX,7,NUMB+5
CP,20,UY,7,NUMB+5
CP,21,UZ,7,NUMB+5
CP,22,ROTX,7,NUMB+5
CP,23,ROTY,7,NUMB+5
CP,24,ROTZ,7,NUMB+5

CP,25,UX,NUMB+6,NUMB2+1
CP,26,UY,NUMB+6,NUMB2+1
CP,27,UZ,NUMB+6,NUMB2+1
CP,28,ROTX,NUMB+6,NUMB2+1
CP,29,ROTY,NUMB+6,NUMB2+1
CP,30,ROTZ,NUMB+6,NUMB2+1

CP,31,UX,NUMB+7,NUMB2+2
CP,32,UY,NUMB+7,NUMB2+2
CP,33,UZ,NUMB+7,NUMB2+2
CP,34,ROTX,NUMB+7,NUMB2+2
CP,35,ROTY,NUMB+7,NUMB2+2
CP,36,ROTZ,NUMB+7,NUMB2+2

CP,37,UX,NUMB+8,NUMB2+3
CP,38,UY,NUMB+8,NUMB2+3
CP,39,UZ,NUMB+8,NUMB2+3
CP,40,ROTX,NUMB+8,NUMB2+3
CP,41,ROTY,NUMB+8,NUMB2+3
CP,42,ROTZ,NUMB+8,NUMB2+3

CP,43,UX,NUMB+9,NUMB2+4
CP,44,UY,NUMB+9,NUMB2+4
CP,45,UZ,NUMB+9,NUMB2+4
CP,46,ROTX,NUMB+9,NUMB2+4
CP,47,ROTY,NUMB+9,NUMB2+4
CP,48,ROTZ,NUMB+9,NUMB2+4

!定义载荷
*IF,JJ,EQ,1,THEN  !工况1
F,((N2+1)*8+1),FZ,-FFZ*10000
F,NUMB+1,FX,0
F,NUMB+1,FY,-FFH*10000
F,((N2+1)*8+1),MX,MMT*10000000   
F,((N2+1)*8+1),MY,0
F,((N2+1)*8+1),MZ,MK*10000000   
*ELSEIF,JJ,EQ,2  !工况2
F,((N2+1)*8+1),FZ,-FFZ*10000
F,NUMB+1,FX,(FFH/1.414)*10000
F,NUMB+1,FY,-(FFH/1.414)*10000
F,((N2+1)*8+1),MX,(MMT/1.414)*10000000   
F,((N2+1)*8+1),MY,(MMT/1.414)*10000000  
F,((N2+1)*8+1),MZ,MK*10000000  
*ELSEIF,JJ,EQ,3  !工况3
F,((N2+1)*8+1),FZ,-FFZ*10000
F,NUMB+1,FX,FFH*10000
F,NUMB+1,FY,0
F,((N2+1)*8+1),MX,0  
F,((N2+1)*8+1),MY,MMT*10000000
F,((N2+1)*8+1),MZ,MK*10000000   
*ELSEIF,JJ,EQ,4  !工况4
F,((N2+1)*8+1),FZ,-FFZ*10000
F,NUMB+1,FX,(FFH/1.414)*10000
F,NUMB+1,FY,(FFH/1.414)*10000
F,((N2+1)*8+1),MX,-(MMT/1.414)*10000000   
F,((N2+1)*8+1),MY,(MMT/1.414)*10000000
F,((N2+1)*8+1),MZ,MK*10000000   
*ELSEIF,JJ,EQ,5  !工况5
F,((N2+1)*8+1),FZ,-FFZ*10000
F,NUMB+1,FX,0
F,NUMB+1,FY,FFH*10000
F,((N2+1)*8+1),MX,-MMT*10000000   
F,((N2+1)*8+1),MY,0
F,((N2+1)*8+1),MZ,MK*10000000  
*ELSEIF,JJ,EQ,6  !工况6
F,((N2+1)*8+1),FZ,-FFZ*10000
F,NUMB+1,FX,-(FFH/1.414)*10000
F,NUMB+1,FY,(FFH/1.414)*10000
F,((N2+1)*8+1),MX,-(MMT/1.414)*10000000   
F,((N2+1)*8+1),MY,-(MMT/1.414)*10000000 
F,((N2+1)*8+1),MZ,MK*10000000  
*ELSEIF,JJ,EQ,7  !工况7
F,((N2+1)*8+1),FZ,-FFZ*10000
F,NUMB+1,FX,-FFH*10000
F,NUMB+1,FY,0
F,((N2+1)*8+1),MX,0  
F,((N2+1)*8+1),MY,-MMT*10000000 
F,((N2+1)*8+1),MZ,MK*10000000  
*ELSE  !工况8
F,((N2+1)*8+1),FZ,-FFZ*10000
F,NUMB+1,FX,-(FFH/1.414)*10000
F,NUMB+1,FY,-(FFH/1.414)*10000
F,((N2+1)*8+1),MX,(MMT/1.414)*10000000   
F,((N2+1)*8+1),MY,-(MMT/1.414)*10000000 
F,((N2+1)*8+1),MZ,MK*10000000  
*ENDIF


!求解
/SOLU
!ACEL,,,9800*1.3  !重力加速度，若考虑自重则去掉前面的感叹号
ALLSEL,ALL
NLGEOM,OFF!打开大变形开关
SOLVE

!后处理提取结果数据
/POST1
!需要提取的结果：
TP1=0
TP2=0
TP3=0
TP4=0
TP5=0
TP6=0
*DIM,NM,,4
NM(1)=NUMB2+1+ZN*4
NM(2)=NUMB2+2+ZN*4
NM(3)=NUMB2+3+ZN*4
NM(4)=NUMB2+4+ZN*4

*CFOPEN,GLresult,txt,,Append

*VWRITE,JJ
('工况',F8.0)
*VWRITE,
('节点号|FX(t)|FY(t)|FZ(t)|MX(t.m)|MY(t.m)|MZ(t.m)')

*DO,I,1,4
*GET,TP1,NODE,NM(I),RF,FX
*GET,TP2,NODE,NM(I),RF,FY
*GET,TP3,NODE,NM(I),RF,FZ
*GET,TP4,NODE,NM(I),RF,MX
*GET,TP5,NODE,NM(I),RF,MY
*GET,TP6,NODE,NM(I),RF,MZ
*VWRITE,NM(I)/1,TP1/10000,TP2/10000,TP3/10000,TP4/10000000,TP5/10000000,TP6/10000000
(F8.0,'|',F8.2,'|',F8.2,'|',F8.2,'|',F8.2,'|',F8.2,'|',F8.2)
*ENDDO

*CFCLOS

!ESEL,S,ELEM,,N2*18+21,N2*18+20+ZN*16
/DSCALE,ALL,1.0
EPLOT
!/EDGE,1,0,45
!/GLINE,1,-1 
/VIEW,1,-1,-1,1   
/ANG,1,60,ZS,1
/AUTO,1
/REP,FAST

!PLNSOL,S,EQV
PLNSOL,U,X
/SHOW,PNG,,0
PNGR,COMP,1,-1  
PNGR,ORIENT,HORIZ   
PNGR,COLOR,2
PNGR,TMOD,1 
/GFILE,800, 
!*  
/CMAP,_TEMPCMAP_,CMP,,SAVE  
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15 
/REPLOT 
/CMAP,_TEMPCMAP_,CMP
/DELETE,_TEMPCMAP_,CMP  
/SHOW,CLOSE 
/DEVICE,VECTOR,0
!*  
PLNSOL,U,Y
/SHOW,PNG,,0
PNGR,COMP,1,-1  
PNGR,ORIENT,HORIZ   
PNGR,COLOR,2
PNGR,TMOD,1 
/GFILE,800, 
!*  
/CMAP,_TEMPCMAP_,CMP,,SAVE  
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15 
/REPLOT 
/CMAP,_TEMPCMAP_,CMP
/DELETE,_TEMPCMAP_,CMP  
/SHOW,CLOSE 
/DEVICE,VECTOR,0
!*  
!输出图片结束

!输出格构柱
ESEL,S,ELEM,,N2*18+93,N2*18+92+ZN*16
/DSCALE,ALL,1.0
EPLOT
!/EDGE,1,0,45
!/GLINE,1,-1 
/VIEW,1,-1,-1,1 
!/ANG,1,60,ZS,1
/AUTO,1
/REP,FAST

PLNSOL,S,EQV
!PLNSOL,U,SUM
/SHOW,PNG,,0
PNGR,COMP,1,-1  
PNGR,ORIENT,HORIZ   
PNGR,COLOR,2
PNGR,TMOD,1 
/GFILE,800, 
!*  
/CMAP,_TEMPCMAP_,CMP,,SAVE  
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15 
/REPLOT 
/CMAP,_TEMPCMAP_,CMP
/DELETE,_TEMPCMAP_,CMP  
/SHOW,CLOSE 
/DEVICE,VECTOR,0
!*  

PLNSOL,U,Z
/SHOW,PNG,,0
PNGR,COMP,1,-1  
PNGR,ORIENT,HORIZ   
PNGR,COLOR,2
PNGR,TMOD,1 
/GFILE,800, 
!*  
/CMAP,_TEMPCMAP_,CMP,,SAVE  
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15 
/REPLOT 
/CMAP,_TEMPCMAP_,CMP
/DELETE,_TEMPCMAP_,CMP  
/SHOW,CLOSE 
/DEVICE,VECTOR,0
!*  
!输出图片结束

!输出十字梁
ESEL,S,ELEM,,N2*18+13,N2*18+92
/DSCALE,ALL,1.0
EPLOT
!/EDGE,1,0,45
!/GLINE,1,-1 
/VIEW,1,0,-1,1
/ANG,1,-60,ZS,1
/AUTO,1
/REP,FAST

PLNSOL,S,EQV
!PLNSOL,U,SUM
/SHOW,PNG,,0
PNGR,COMP,1,-1  
PNGR,ORIENT,HORIZ   
PNGR,COLOR,2
PNGR,TMOD,1 
/GFILE,800, 
!*  
/CMAP,_TEMPCMAP_,CMP,,SAVE  
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15 
/REPLOT 
/CMAP,_TEMPCMAP_,CMP
/DELETE,_TEMPCMAP_,CMP  
/SHOW,CLOSE 
/DEVICE,VECTOR,0
!*  
PLNSOL,U,Z
/SHOW,PNG,,0
PNGR,COMP,1,-1  
PNGR,ORIENT,HORIZ   
PNGR,COLOR,2
PNGR,TMOD,1 
/GFILE,800, 
!*  
/CMAP,_TEMPCMAP_,CMP,,SAVE  
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15 
/REPLOT 
/CMAP,_TEMPCMAP_,CMP
/DELETE,_TEMPCMAP_,CMP  
/SHOW,CLOSE 
/DEVICE,VECTOR,0
!*  
!输出图片结束

!循环求解结束
! *ENDDO
